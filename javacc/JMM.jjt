options {
    LOOKAHEAD = 1;
    FORCE_LA_CHECK = true;
    NODE_SCOPE_HOOK = true;
}

PARSER_BEGIN(JMM)

import java.io.FileInputStream;
import java.io.FileNotFoundException;
import java.util.ArrayList;
import pt.up.fe.comp.jmm.report.Report;
import pt.up.fe.comp.jmm.report.ReportType;
import pt.up.fe.comp.jmm.report.Stage;
import java.util.List;

public class JMM {
    public static void main(String[] args) throws ParseException, FileNotFoundException {
        JMM jmm = new JMM(new FileInputStream(args[0]));
        SimpleNode root = jmm.Program(); // returns reference to root node

        root.dump(""); // prints the tree on the screen
    }

    void jjtreeOpenNodeScope(Node n) {
        ((SimpleNode) n).put("line", Integer.toString(getToken(1).beginLine));
        ((SimpleNode) n).put("col", Integer.toString(getToken(1).beginColumn));       
    }

    void jjtreeCloseNodeScope(Node n) {}
}

PARSER_END(JMM)

SKIP : {
    " " | "\r" | "\t" | "\n" | < "//" (~["\r", "\n"])* >
    | "/*" : ML_COMMENT_STATE
}

<ML_COMMENT_STATE> SKIP :
{
    "*/" : DEFAULT
    | < ~[] >
}

TOKEN : {
    <IMPORT: "import">
    | <CLASS: "class">
    | <EXTENDS: "extends">
    | <PUBLIC: "public">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <RETURN: "return">
    | <STATIC: "static">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRING: "String">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <LENGTH: "length">
    | <TRUE: "true">
    | <FALSE: "false">
    | <THIS: "this">
    | <NEW: "new">
    | <IDENTIFIER: (["a"-"z", "A"-"Z", "_", "$"](["a"-"z", "A"-"Z", "0"-"9", "_", "$"])+ | ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z", "0"-"9", "_", "$"])*)>
    | <INTEGER_LITERAL: ["0"-"9"](["0"-"9"])*>
    | <ASSIGN: "=">
    | <LBR: "{">
    | <RBR: "}">
    | <LSQBR: "[">
    | <RSQBR: "]">
    | <LPAR: "(">
    | <RPAR: ")">
    | <DOT: ".">
    | <COMMA: ",">
    | <SEMICOLON: ";">
    | <AND: "&&">
    | <LT: "<">
    | <ADD: "+">
    | <SUB: "-">
    | <MUL: "*">
    | <DIV: "/">
    | <NOT: "!">
}

SimpleNode Program() : {} {
    ImportDeclaration() ClassDeclaration() <EOF> { return jjtThis; }
}

void ImportDeclaration() #Imports : { Token t; } {
    (Import())*
}

void Import() #Import : { Token t; String importName = ""; } {
    <IMPORT> t = <IDENTIFIER> {
        importName += t.image;
    } (<DOT> t = <IDENTIFIER> {
        importName += "." + t.image;
    })* <SEMICOLON>
    {
        jjtThis.put("module", importName);
    }
}

void ClassDeclaration() #Class : { Token t; } {
    <CLASS> t = <IDENTIFIER> { jjtThis.put("name", t.image); }
    (<EXTENDS> t = <IDENTIFIER> { jjtThis.put("extends", t.image); })?
    <LBR> (VarDeclaration())* (MethodDeclaration())* <RBR>
}

void VarDeclaration() #VarDecl : { Token t; String varType; } {
    varType = Type() t = <IDENTIFIER> <SEMICOLON>
    {
        jjtThis.put("type", varType);
        jjtThis.put("name", t.image);
    }
}

void MethodDeclaration() #void : {} {
    <PUBLIC> MethodDeclarationAux()
}

void MethodDeclarationAux() #Method : { Token t; String returnType; } {
    (returnType = Type() t = <IDENTIFIER> {
        jjtThis.put("returnType", returnType);
        jjtThis.put("name", t.image);
    } <LPAR> MethodParameters() <RPAR> <LBR> MethodBody() MethodReturn() <RBR>)
    | (<STATIC> t = <VOID> { returnType = t.image; } t = <MAIN> {
        jjtThis.put("returnType", returnType);
        jjtThis.put("name", t.image);
    } <LPAR> <STRING> <LSQBR> <RSQBR> t = <IDENTIFIER> {
        jjtThis.put("cmdArgsName", t.image);
    } <RPAR> <LBR>
    MethodBody() <RBR>)
}

void MethodParameters() #Params : {} {
    (MethodParameter() (<COMMA> MethodParameter())*)?
}

void MethodParameter() #Param : { Token t; String type; } {
    type = Type() t = <IDENTIFIER> {
        jjtThis.put("type", type);
        jjtThis.put("name", t.image);
    }
}

void MethodBody() #Body : {} {
    (LOOKAHEAD(2) VarDeclaration())* (Statement())*
}

void MethodReturn() #Return : {} {
    <RETURN> Expression() <SEMICOLON>
}

String Type() #void : {Token t;} {
    t = <INT> (<LSQBR> t = <RSQBR>)? { if (t.image == "]") return "int[]"; else return "int"; }
    | t = <BOOLEAN> { return t.image; }
    | t = <IDENTIFIER> { return t.image; }
}

void Statement() : {} {
    (<LBR> (Statement())* <RBR>)
    | IfStatement()
    | WhileStatement()
    //| LOOKAHEAD(2) Assignment() <SEMICOLON>
    | Expression() <SEMICOLON>
}

void IfStatement() #If : {} {
    <IF> <LPAR> Expression() <RPAR> Statement() #Then <ELSE> Statement() #Else
}

void WhileStatement() #While : {} {
    try {
        <WHILE> <LPAR> Expression() #Condition <RPAR>
    }
    catch (ParseException ex) {
        Reports.store(new Report(ReportType.ERROR, Stage.SYNTATIC, ex.currentToken.endLine, ex.getMessage()));
        System.out.println(ex.getMessage());
        skipToStatement();
    } Statement() #Body
}

JAVACODE
void skipToStatement() {
    Token t = null;

    /*  Ignores tokens until a LBR token is found (but doesn't consume it) or until the last RPAR token is found
        in a chain of RPAR tokens */

    while (getToken(1).kind != LBR && (t == null || t.kind != RPAR || getToken(1).kind == RPAR)) {
        t = getNextToken();
    }
}

void Assignment() : {} {
    <IDENTIFIER> (<LSQBR> Expression() <RSQBR>)? <ASSIGN> Expression()
}

void Expression() : {} {
    ExpressionAssign()
}

void ExpressionAssign() #void : {} {
    ExpressionAnd() (<ASSIGN> ExpressionAnd() #Assign(2))?
}

void ExpressionAnd() #void : {} {
    ExpressionLess() (<AND> ExpressionLess() #And(2))*
}

void ExpressionLess() #void : {} {
    ExpressionAddSub() (<LT> ExpressionAddSub() #LessThan(2))*
}

void ExpressionAddSub() #void : {} {
    ExpressionMulDiv() ((<ADD> ExpressionMulDiv() #Add(2) | <SUB> ExpressionMulDiv() #Sub(2)))*
}

void ExpressionMulDiv() #void : {} {
    ExpressionNot() ((<MUL> ExpressionNot() #Mul(2) | <DIV> ExpressionNot() #Div(2)))*
}

void ExpressionNot() #void : {} {
    <NOT> ExpressionNot() #Not(1)
    | Primary()
}

void Primary() #void : { Token t; } {
    <LPAR> ExpressionAssign() <RPAR> PrimaryL()
    | t = <INTEGER_LITERAL> { jjtThis.put("value", t.image); } #Int PrimaryL()
    | t = <IDENTIFIER> { jjtThis.put("name", t.image); } #Var PrimaryL()
    | <THIS> #This PrimaryL()
    | <NEW> ExpressionNew() PrimaryL()
    | <TRUE> #True PrimaryL()
    | <FALSE> #False PrimaryL()
}

void PrimaryL() #void : {} {
    (<LSQBR> Expression() <RSQBR>) #ArrayAccess(2) PrimaryL()
    | <DOT> ExpressionDot() #Dot(2) PrimaryL()
    | {}
}

void ExpressionNew() #void : {} {
    NewArray()
    | NewInstance()
}

void NewArray() : {} {
    <INT> <LSQBR> Expression() #Size <RSQBR>
}

void NewInstance() : { Token t; } {
    t = <IDENTIFIER> { jjtThis.put("class", t.image); } <LPAR> <RPAR>
}

void ExpressionDot() #void : {  } {
    Function() | <LENGTH> #Length
}

void Function() #Func : { Token t; } {
    t = <IDENTIFIER> { jjtThis.put("name", t.image); } <LPAR> Arguments() <RPAR>
}

void Arguments() #Args : {} {
    (ExpressionAssign() (<COMMA> ExpressionAssign())*)?
}
